[FUNCTION accountinfo]
IF (<ISEMPTY <ARGS>>)
NEWITEM=i_account_memory
NEW.EQUIP
RETURN 1
ENDIF
f_accountinfo <ARGS>

[FUNCTION f_accountinfo]
IF !STRCMPI(<ARGS>,<NAME>)
OBJ=<UID>
ELSE
VAR.TELEFLAGS=<FLAGS>
IF !(<FLAGS>&statf_insubstantial)
FLAGS=<FLAGS>|statf_insubstantial
ENDIF

LOCAL.BACKP=<P>
GONAME <ARGS>
OBJ=<ACT.UID>
P=<LOCAL.BACKP>
FLAGS=<VAR.TELEFLAGS>
UPDATE
ENDIF

TRYSRC <UID> DIALOG d_accinfo


[ITEMDEF i_account_memory]
ID=i_memory
TYPE=t_eq_script
NAME=Accountinfo memory

ON=@EQUIP
TARGET Whose accountinfo do you wish to view?

ON=@TARGON_CHAR
SRC.f_accountinfo <SRC.TARG.NAME>
REMOVE
RETURN 1

ON=@TARGON_ITEM
REMOVE
RETURN 1

ON=@TARGON_GROUND
TRIGGER @TARGON_ITEM

ON=@TARGON_CANCEL
TRIGGER @TARGON_ITEM


[DIALOG d_accinfo] 
0,0
resizepic 0 0 3600 300 503 
resizepic 350 0 3600 300 503 

tilepic 245 20 <EVAL(<OBJ.ACCOUNT.CHAR.<VAR.CHARNR>.ICON>)> 
dtext 20 15 1152 Char # <EVAL(<VAR0.CHARNR> + 1)>
dtext 120 15 1152 Chars total: <OBJ.ACCOUNT.CHARS>

dtext 20 55 180 Name
dtextentry 90 55 220 20 1152 0 <OBJ.ACCOUNT.CHAR.<VAR.CHARNR>.NAME>
dtext 20 75 180 Title
dtextentry 90 75 220 20 1152 1 <OBJ.ACCOUNT.CHAR.<VAR.CHARNR>.Title>
dtext 20 95 180 UID
dtext 90 95 1152 <OBJ.ACCOUNT.CHAR.<VAR.CHARNR>.UID>
dtext 20 115 180 Status
dtext 90 115 1152 <QVAL <OBJ.ACCOUNT.CHAR.<VAR.CHARNR>.ISONLINE> ? Online : Offline> 
dtext 20 145 180 P
dtext 90 145 1152 <OBJ.ACCOUNT.CHAR.<VAR.CHARNR>.P> (<OBJ.ACCOUNT.CHAR.<VAR.CHARNR>.REGION.NAME>)
dtext 20 165 180 Guild
dtext 90 165 1152 <OBJ.ACCOUNT.CHAR.<VAR.CHARNR>.MEMORYFINDTYPE.memory_guild.LINK.ABBREV>
dtext 20 185 180 Karma
dtextentry 90 185 220 20 1152 2 <OBJ.ACCOUNT.CHAR.<VAR.CHARNR>.KARMA>
dtext 20 205 180 Fame
dtextentry 90 205 220 20 1152 3 <OBJ.ACCOUNT.CHAR.<VAR.CHARNR>.FAME>
dtext 125 235 180 Events
dhtmlgump 20 255 260 65 20 1 <OBJ.ACCOUNT.CHAR.<VAR.CHARNR>.EVENTS>

dtext 50 327 1152 Open characters bankbox
dtext 50 352 1152 Open characters backpack
dtext 50 377 1152 Summon character
dtext 50 402 1152 Goto character
dtext 50 427 1152 Run function on character

button 25 330 1209 1210 1 0 3 // Bank
button 25 355 1209 1210 1 0 4 // Backpack
button 25 380 1209 1210 1 0 5 // Summon
button 25 405 1209 1210 1 0 6 // Goto
button 25 430 1209 1210 1 0 7 // Function

button 80 460 2124 2123 1 0 1 // Apply
button 160 460 2121 2120 1 0 2 // Cancel

button 25 460 4014 4016 1 0 8 // Left arrow
button 245 460 4005 4007 1 0 9 // Right arrow


dtext 370 15 180 Account
dtext 460 15 1152 <OBJ.ACCOUNT>
dtext 370 35 180 Plevel
dtext 460 35 1152 <OBJ.ACCOUNT.PLEVEL>
dtext 370 55 180 Status
dtext 460 55 1152 <OBJ.ACCOUNT.STATUS>
dtext 370 75 180 Email
dtext 460 75 1152 <OBJ.ACCOUNT.EMAIL>

dtext 370 115 180 First connection date
dtext 370 135 1152 <OBJ.ACCOUNT.FIRSTCONNECTDATE>
dtext 370 155 180 Last connection date
dtext 370 175 1152 <OBJ.ACCOUNT.LASTCONNECTDATE>
dtext 370 195 180 Last Character logged
dtext 370 215 1152 <UID.<OBJ.ACCOUNT.LASTCHARUID>.NAME>
dtext 370 235 180 Total connect time (mins)
dtext 370 255 1152 <OBJ.ACCOUNT.TOTALCONNECTTIME>
dtext 370 275 180 Last connect time (mins)
dtext 370 295 1152 <OBJ.ACCOUNT.LASTCONNECTTIME>

dtext 370 335 180 Password
dtextentry 370 355 220 20 1152 4 <QVAL (<SRC.ACCOUNT.PLEVEL> == 7) ? <OBJ.ACCOUNT.PASSWORD> : N/A>

dtext 400 387 1152 Block/unblock account
dtext 400 412 1152 Jail/Pardon account
dtext 400 437 1152 Delete account

button 375 390 1209 1210 1 0 10 // Block
button 375 415 1209 1210 1 0 11 // Jail
button 375 440 1209 1210 1 0 12 // Delete

dtext 440 470 550 Made by Tabain

[DIALOG d_accinfo BUTTON] 
ON=0
VAR.CHARNR=
RETURN 1

ON=1 
TRY OBJ.ACCOUNT.CHAR.<VAR.CHARNR>.NAME=<ARGTXT[0]>
TRY OBJ.ACCOUNT.CHAR.<VAR.CHARNR>.TITLE=<ARGTXT[1]>
TRY OBJ.ACCOUNT.CHAR.<VAR.CHARNR>.KARMA=<ARGTXT[2]>
TRY OBJ.ACCOUNT.CHAR.<VAR.CHARNR>.FAME=<ARGTXT[3]>
IF (<ACCOUNT.PLEVEL> == 7)
OBJ.ACCOUNT.PASSWORD=<ARGTXT[4]>
ENDIF
DIALOG d_accinfo

ON=2
VAR.CHARNR=
RETURN 1

ON=3 
TRY OBJ.ACCOUNT.CHAR.<VAR.CHARNR>.FINDLAYER.29.OPEN
VAR.CHARNR=
RETURN 1


ON=4 
TRY OBJ.ACCOUNT.CHAR.<VAR.CHARNR>.FINDLAYER.21.OPEN
VAR.CHARNR=
RETURN 1

ON=5
TRY OBJ.ACCOUNT.CHAR.<VAR.CHARNR>.GONAME <NAME>
VAR.CHARNR=
RETURN 1

ON=6
GONAME <OBJ.ACCOUNT.CHAR.<VAR.CHARNR>.NAME>
VAR.CHARNR=
RETURN 1

ON=7
TARG <OBJ.ACCOUNT.CHAR.<VAR.CHARNR>.UID>
EVERBTARG 
VAR.CHARNR=
RETURN 1

ON=8
IF (!<VAR.CHARNR>)
DIALOG d_accinfo
ELSE
VAR.CHARNR=<EVAL(<VAR0.CHARNR> - 1)>
DIALOG d_accinfo
ENDIF

ON=9
IF (<VAR.CHARNR> == <EVAL(<OBJ.ACCOUNT.CHARS> - 1)>)
DIALOG d_accinfo
ELSE
VAR.CHARNR=<EVAL(<VAR0.CHARNR> + 1)>
DIALOG d_accinfo
ENDIF

ON=10
IF (<ACCOUNT.PLEVEL> > 4)
DOSWITCH <OBJ.ACCOUNT.BLOCK>
OBJ.ACCOUNT.BLOCK 1
OBJ.ACCOUNT.BLOCK 0
ENDDO
DIALOG d_accinfo
ELSE
SYSMESSAGE You can not block accounts.
DIALOG d_accinfo
ENDIF

ON=11
IF (<ACCOUNT.PLEVEL> > 2)
DOSWITCH <OBJ.ACCOUNT.JAIL>
OBJ.ACCOUNT.JAIL 1
OBJ.ACCOUNT.JAIL 0
ENDDO
DIALOG d_accinfo
ELSE
SYSMESSAGE You can not jail accounts.
DIALOG d_accinfo
ENDIF

ON=12
IF (<ACCOUNT.PLEVEL> == 7)
OBJ.ACCOUNT.DELETE
ELSE
SYSMESSAGE You can not delete accounts.
DIALOG d_accinfo
ENDIF

